/*
	Shader controller base class

	This allows mappers to control shaders using the GiveInventory/TakeInventory
	ACS functions.

	Do not use this directly! Instead, make an actor class that inherits from
	ShaderControl. Set the ShaderControl.Shader property to the name of the
	shader to control. The shader must be defined in GLDEFS.

	To enable a shader, give 2 of the ShaderControl subclass item to the player.
	For example:

	GiveInventory("ShakeShaderControl", 2);
	
	To disable a shader, take 1 of the shader control item away so that the
	player only has 1 of said item. For example:

	TakeInventory("ShakeShaderControl", 1);
	or
	SetInventory("ShakeShaderControl", 1);

	Note that the latter requires you to define the SetInventory function in
	your ACS code.
*/
class ShaderControl : Inventory {
	string ShaderToControl;
	property Shader: ShaderToControl;
	Default {
		Inventory.MaxAmount 2;
	}

	virtual ui void SetUniforms(PlayerInfo p, RenderEvent e) {}
}

class CustomShaderHandler : StaticEventHandler
{
	override void RenderOverlay(RenderEvent e)
	{
      // thanks to Pixel Eater from the ZDoom Forums!!!
        float timer = (gametic +  e.FracTic) * 1 / 35;
	    PlayerInfo p = players[consoleplayer];
		ShaderControl shaderControl = ShaderControl(p.mo.FindInventory("ShaderControl", true));

		if (shaderControl != null)
		{
			//Console.Printf("Shader: %s", shaderControl.ShaderToControl);
			if (shaderControl.amount >= 2)
			{
				Shader.SetUniform1f(p, shaderControl.ShaderToControl, "timer",( gametic + e.FracTic ) * 1 / 35 ) ;
				shaderControl.SetUniforms(p, e);
				Shader.SetEnabled(p, shaderControl.ShaderToControl, true);
			}
			else
			{
				Shader.SetEnabled(p, shaderControl.ShaderToControl, false);
			}
		}
	}	
//this does nothing at the moment but might be useful in case we add more shaders (it's used to disable them on load/newgame)
  override void WorldLoaded(WorldEvent e)
  {
    PlayerInfo p = players[consoleplayer];
    Shader.SetEnabled(p, "tvdistortionshader", false);
  }

  override void WorldUnloaded(WorldEvent e)
  {
    PlayerInfo p = players[consoleplayer];
    Shader.SetEnabled(p, "tvdistortionshader", false);
  }
}

//This is where the inherited classes start

class TVDistShaderControl : ShaderControl {
	Default {
		ShaderControl.Shader "tvdistortionshader";
	}
}












